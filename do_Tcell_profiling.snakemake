try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"


###########
# Targets #
###########

MITCR_TXT_FILES = ["miTCR/" + s + ".txt" for s in INPUT_CFG["fastq_base"]]
MITCR_CLS_FILES = ["miTCR/" + s + ".cls" for s in INPUT_CFG["fastq_base"]]
MITCR_PLOT_FILES = ["miTCR/plots/" + s for s in ["distribution_of_reads_per_sample_density.pdf","distribution_of_reads_per_sample_freqpoly.pdf",
"distribution_of_reads_per_sample_histogram.pdf","number_of_sequences_per_sample.pdf","total_reads_per_sample.pdf"]]


#########
# Rules #
#########

shell.prefix("set -evuf -o pipefail;")
ruleorder: make_miTCR_plots > run_miTCR
localrules: link_miTCR_fastq_files,zip_miTCR_results


rule do_miTCR:
	input:
		MITCR_TXT_FILES,
		MITCR_CLS_FILES,
		MITCR_PLOT_FILES,
		"miTCR/percentage_of_reads_used_in_MiTCR.txt",
		"miTCR/miTCR_analysis.zip"

rule zip_miTCR_results:
	input:
		MITCR_TXT_FILES,
		MITCR_CLS_FILES,
		MITCR_PLOT_FILES,
		"miTCR/percentage_of_reads_used_in_MiTCR.txt"
	output:
		"miTCR/miTCR_analysis.zip"
	shell:
		"""
		zip {output} {input}
		"""

rule make_miTCR_plots:
	input:
		MITCR_TXT_FILES,
		MITCR_CLS_FILES,
		MITCR_FASTQ_FILES
	output:
		MITCR_PLOT_FILES,
		"miTCR/percentage_of_reads_used_in_MiTCR.txt"
	log:
		"miTCR/log/make_miTCR_plots.log"
	shell:
		"""
		module load apps/R
		cd miTCR
		(Rscript /group/sequencing/Bfx/scripts/common/plotMiTCRResults.R) >& ../{log}
		"""

rule run_miTCR:
	input:
		"fastq/{basename}.fastq.gz"
	output:
		txt="miTCR/{basename}.txt",
		cls="miTCR/{basename}.cls"
	log:
		"miTCR/log/{basename}.log"
	threads:
		4
	version:
		"1.0.3"
	shell:
		"""
		java -Xmx1500m -jar /share/apps/mitcr/1.0.3/mitcr.jar -t ${{NSLOTS:-{threads}}} -pset {config[miTCR_preset]} -gene {config[miTCR_chain]} -ec {config[miTCR_ec]} {input} {output.txt} >& {log}
		java -Xmx1500m -jar /share/apps/mitcr/1.0.3/mitcr.jar -t ${{NSLOTS:-{threads}}} -pset {config[miTCR_preset]} -gene {config[miTCR_chain]} -ec {config[miTCR_ec]} {input} {output.cls} 2>> {log} 1>&2
		"""


