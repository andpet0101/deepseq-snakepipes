try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"

####################
# Config/Parameter #
####################

VARIANT_CALLING_CFG = config.get("variant_calling",dict())

if "base_recalibration_vcfs" in VARIANT_CALLING_CFG:
	for f in VARIANT_CALLING_CFG["base_recalibration_vcfs"]:
		if not os.path.exists(f):
			raise WorkflowError("Could not find file "+f+" for variant_calling.base_recalibration_vcfs!")
else:
	VARIANT_CALLING_CFG["base_recalibration_vcfs"] = []

# path to picard jar
if "picardtools" not in PROGRAM_CFG:
	PROGRAM_CFG["picardtools"]="/share/apps/picard_tools/2.5.0/picard.jar"

# path to GATK jar
if "gatk" not in PROGRAM_CFG:
	PROGRAM_CFG["gatk"] = "/share/apps/GenomeAnalysisTK/3.4-0/GenomeAnalysisTK.jar"


###########
# Targets #
###########

# see first rule


#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

rule do_variant_calling_bam_preprocessing:
	input:
		bam=[ALIGNER_CFG["program"] + "/" + s + ".variant_calling.bam" for s in LIBRARIES],
		bai=[ALIGNER_CFG["program"] + "/" + s + ".variant_calling.bai" for s in LIBRARIES]

# if recalibration vcf has been provided, look for base-recalibrated bam, else look for indel-realigned bam
rule make_variant_calling_bam:
	input:
		bam="{aligner}/{basename}." + ("recal" if VARIANT_CALLING_CFG["base_recalibration_vcfs"] else "indels") + ".bam",
		bai="{aligner}/{basename}." + ("recal" if VARIANT_CALLING_CFG["base_recalibration_vcfs"] else "indels") + ".bai"

	output:
		bam="{aligner}/{basename}.variant_calling.bam",
		bai="{aligner}/{basename}.variant_calling.bai"
	shell:
		"""
		cp -f {input.bam} {output.bam}
		cp -f {input.bai} {output.bai}
		"""

# recalibrate base qualities (if recalibration vcf has been provided)
rule run_GATK_BaseRecalibrator_PrintReads:
	input:
		bam="{aligner}/{basename}.indels.bam",
		bai="{aligner}/{basename}.indels.bai",
		table="{aligner}/report/data/"+PROJECT+"_{basename}.recal.txt"
	output:
		bam=temp("{aligner}/{basename}.recal.bam"),
		bai=temp("{aligner}/{basename}.recal.bai")
	log:
		"{aligner}/log/{basename}.recal.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx2g -jar {PROGRAM_CFG[gatk]} -T PrintReads -R {REFERENCE_CFG[path]}.fa -I {input.bam} -BQSR {input.table} -o {output.bam}) >& {log}
		(java -Xmx2g -jar {PROGRAM_CFG[picardtools]} BuildBamIndex I={output.bam} VALIDATION_STRINGENCY=SILENT TMP_DIR={TMP_DIR}) 2>> {log} 1>&2
		""" 

# run base recalibrator (if recalibration vcf has been provided)
rule run_GATK_BaseRecalibrator:
	input:
		bam="{aligner}/{basename}.indels.bam",
		bai="{aligner}/{basename}.indels.bai",
		check="{aligner}/vcf_integrity_check"
	output:
		temp("{aligner}/report/data/"+PROJECT+"_{basename}.recal.txt")
	log:
		"{aligner}/log/{basename}.recal.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx8g -jar {PROGRAM_CFG[gatk]} -T BaseRecalibrator -R {REFERENCE_CFG[path]}.fa -I {input.bam} -knownSites """+(" -knownSites ".join(VARIANT_CALLING_CFG["base_recalibration_vcfs"]))+""" -o {output}) >& {log}
		"""

		
# realigns indels in target intervals
rule run_GATK_IndelRealigner:
	input:
		bam="{aligner}/{basename}.markdup.bam",
		bai="{aligner}/{basename}.markdup.bai",
		intervals="{aligner}/{basename}.indels.intervals"
	output:
		bam=temp("{aligner}/{basename}.indels.bam"),
		bai=temp("{aligner}/{basename}.indels.bai")
	log:
		"{aligner}/log/{basename}.indelrealign.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx12g -jar {PROGRAM_CFG[gatk]} -T IndelRealigner -R {REFERENCE_CFG[path]}.fa -I {input.bam} -targetIntervals {input.intervals} -o {output.bam} --filter_reads_with_N_cigar) >& {log}
		(java -Xmx2g -jar {PROGRAM_CFG[picardtools]} BuildBamIndex I={output.bam} VALIDATION_STRINGENCY=SILENT TMP_DIR={TMP_DIR}) 2>> {log} 1>&2
		"""

# creates target intervals for indel realignment in wgs/exome data
rule run_GATK_RealignerTargetCreator_wgs:
	input:
		bam="bwa/{basename}.markdup.bam",
		bai="bwa/{basename}.markdup.bai"
	output:
		temp("bwa/{basename}.indels.intervals")
	log:
		"bwa/log/{basename}.indelrealign.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx8g -jar {PROGRAM_CFG[gatk]} -T RealignerTargetCreator -R {REFERENCE_CFG[path]}.fa -I {input.bam} -o {output}) >& {log}
		"""

# creates target intervals for indel realignment in rnaseq data
rule run_GATK_RealignerTargetCreator_rnaseq:
	input:
		bam="gsnap/{basename}.splitN.bam",
		bai="gsnap/{basename}.splitN.bai"
	output:
		temp("gsnap/{basename}.indels.intervals")
	log:
		"gsnap/log/{basename}.indelrealign.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx8g -jar {PROGRAM_CFG[gatk]} -T RealignerTargetCreator -R {REFERENCE_CFG[path]}.fa -I {input.bam} -o {output}) >& {log}
		"""

# splits rnaseq alignments with the cigar operator N (indicating introns)
rule run_GATK_SplitNCigarReads:
	input:
		bam="gsnap/{basename}.fixmate.bam",
		bai="gsnap/{basename}.fixmate.bai"
	output:
		bam=temp("gsnap/{basename}.splitN.bam"),
		bai=temp("gsnap/{basename}.splitN.bai")
	log:
		"gsnap/log/{basename}.splitN.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx8g -jar {PROGRAM_CFG[gatk]} -T SplitNCigarReads -R {REFERENCE_CFG[path]}.fa -I {input.bam} -o {output.bam} -rf ReassignOneMappingQuality -RMQF 255 -RMQT 60 -U ALLOW_N_CIGAR_READS) >& {log}
		(java -Xmx2g -jar {PROGRAM_CFG[picardtools]} BuildBamIndex I={output.bam} VALIDATION_STRINGENCY=SILENT TMP_DIR={TMP_DIR}) 2>> {log} 1>&2
		"""

# marks read duplicates
rule run_MarkDuplicates:
	input:
		bam="{aligner}/{basename}.fixmate.bam",
		bai="{aligner}/{basename}.fixmate.bai"
	output:
		bam=temp("{aligner}/{basename}.markdup.bam"),
		bai=temp("{aligner}/{basename}.markdup.bai"),
		csv="{aligner}/report/data/"+PROJECT+"_{basename}.markdup.csv"
	log:
		"{aligner}/log/{basename}.markdup.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx8g -XX:-UseGCOverheadLimit -jar {PROGRAM_CFG[picardtools]} MarkDuplicates I={input.bam} O={output.bam} M={output.csv} REMOVE_DUPLICATES=false CREATE_INDEX=true) >& {log}
		"""

# fixes the mate information in wgs/exome data
rule run_FixMateInformation_wgs:
	input:
		"bwa/{basename}.bam"
	output:
		bam=temp("bwa/{basename}.fixmate.bam"),
		bai=temp("bwa/{basename}.fixmate.bai")
	log:
		"bwa/log/{basename}.fixmate.log"
	shell:
		"""
		(java -Djava.io.tmpdir={TMP_DIR} -Xmx2g -jar {PROGRAM_CFG[picardtools]} FixMateInformation I={input} O={output.bam} CREATE_INDEX=true VALIDATION_STRINGENCY=SILENT SO=coordinate) >& {log}
		"""


# fixes the mate information in rnaseq data (also removes problematic reads with the tags below)
rule run_FixMateInformation_rnaseq:
	input:
		"gsnap/{basename}.bam"
	output:
		bam=temp("gsnap/{basename}.fixmate.bam"),
		bai=temp("gsnap/{basename}.fixmate.bai")
	log:
		"gsnap/log/{basename}.fixmate.log"
	shell:
		"\n\t\t("+module_load("samtools")+
		"""
		(samtools view -h {input} | egrep -v "XO:Z:(CT|CC|PM|PS|PI|PL|UT|HT|HM|CM|CC|UM)" | java -Xmx2g -jar {PROGRAM_CFG[picardtools]} FixMateInformation I=/dev/stdin O={output.bam} CREATE_INDEX=true VALIDATION_STRINGENCY=SILENT TMP_DIR={TMP_DIR} SO=coordinate) >& {log}
		"""


# checks the integrity of the VCF files provided for recalibration
rule check_base_recalibration_vcfs:
	output:
		temp("{aligner}/vcf_integrity_check")
	log:
		"{aligner}/log/vcf_integrity_check.log"
	priority:
		100
	shell:
		"""
		(for vcf in """+(" ".join(VARIANT_CALLING_CFG["base_recalibration_vcfs"]))+""";do java -Xmx2g -jar {PROGRAM_CFG[gatk]} -T ValidateVariants -R {REFERENCE_CFG[path]}.fa -V $vcf --validationTypeToExclude ALL;done) >& {log}
		cp {log} {output}
		"""
