try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"

####################
# Config/Parameter #
####################

# info about aligner and libcomplexity is needed
if not ALIGNER_CFG:
	raise WorkflowError("Please provide an aligner configuration!")
if not LIBCOMPLEXITY_CFG:
	raise WorkflowError("Please provide a libcomplexity configuration!")

BEDTOOLS_STRAND_PARAM = "" if STRAND==0 else "-s" if STRAND==1 else "-S"
EXON_EXON_GTF_PARAM = LIBCOMPLEXITY_CFG["exon_exon_gtf"] if "exon_exon_gtf" in LIBCOMPLEXITY_CFG else ""
CALC_GENE_COMPLEXITY = LIBCOMPLEXITY_CFG["calc_gene_complexity"]

###########
# Targets #
###########

# produce bam and bai files
BAM_FILES = [ALIGNER_CFG["program"] + "/" + s + ".bam" for s in LIBRARIES]
BAI_FILES = [ALIGNER_CFG["program"] + "/" + s + ".bam.bai" for s in LIBRARIES]

# produce mapping stats files
MAPPING_STATS_FILES = []
for s in LIBRARIES:
	MAPPING_STATS_FILES.append(ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_" + s + "_alignment_stats.csv")
	MAPPING_STATS_FILES.append(ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_" + s + "_alignment_complexity.csv")

# produce mapping stats plots
MAPPING_PLOTS_FILES = []
for s in ["subsample_number_1","complete_number_1","subsample_percent_1","complete_percent_1"]:
	file_base = PROJECT + "_alignmentcomplexity_" + s + "." + ("Complete_Gene" if LIBCOMPLEXITY_CFG["calc_gene_complexity"] else "Complete") + "." + ALIGNER_CFG["program"]
	MAPPING_PLOTS_FILES.append(ALIGNER_CFG["program"] + "/report/pdf/" + file_base + ".pdf")
	MAPPING_PLOTS_FILES.append(ALIGNER_CFG["program"] + "/report/picture/" + file_base + ".png")

MAPPING_TARGETS = [BAM_FILES,BAI_FILES,MAPPING_PLOTS_FILES]


#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

# paired mapping supercedes single mapping
ruleorder: run_gsnap_paired > run_gsnap_single
ruleorder: run_bwa_paired > run_bwa_single

rule do_mapping:
	input:
		MAPPING_TARGETS

rule make_mapping_plots:
	input:
		MAPPING_STATS_FILES
	output:
		MAPPING_PLOTS_FILES
	log:
		ALIGNER_CFG["program"] + "/log/figures_mappingstatistics.log"
	shell:
		"\n\t\t("+module_load("python","R")+
		"""
		touch {MAPPING_PLOTS_FILES}
		figures_mappingstatistics.py -i {ALIGNER_CFG[program]}/report/data -r {ALIGNER_CFG[program]}/report -s """ + ("Complete Gene" if CALC_GENE_COMPLEXITY==1 else "Complete")  + """ -b {PROJECT} -d {LIBCOMPLEXITY_CFG[lib_display]}) >& {log}
		"""

if CALC_GENE_COMPLEXITY==1:
	rule calculate_mapping_stats:
		input:
			bam=ALIGNER_CFG["program"] + "/{basename}.bam"
		output:
			stats=ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_{basename}_alignment_stats.csv",
			complexity=ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_{basename}_alignment_complexity.csv"
		log:
			ALIGNER_CFG["program"] + "/log/" + PROJECT + "_{basename}.calculate_mapping_stats.log"
		shell:
			"\n\t\t("+module_load("python","bedtools","samtools")+
			"""
			samtools view -F 0x100 -b {input.bam} | calculate_mappingstatistics.py -b {PROJECT} -f -m -l -o {ALIGNER_CFG[program]}/report -s Complete -i {LIBCOMPLEXITY_CFG[subsample_size]}
			samtools view -F 0x100 -b {input.bam} | bedtools intersect {BEDTOOLS_STRAND_PARAM} -nonamecheck -u -f 0.5 -split -a stdin -b {EXON_EXON_GTF_PARAM} | calculate_mappingstatistics.py -b {PROJECT} -f -l -o {ALIGNER_CFG[program]}/report -s Gene -i {LIBCOMPLEXITY_CFG[subsample_size]}) &> {log}
			"""
else:
	rule calculate_mapping_stats:
		input:
			bam=ALIGNER_CFG["program"] + "/{basename}.bam"
		output:
			stats=ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_{basename}_alignment_stats.csv",
			complexity=ALIGNER_CFG["program"] + "/report/data/" + PROJECT + "_{basename}_alignment_complexity.csv"
		log:
			ALIGNER_CFG["program"] + "/log/" + PROJECT + "_{basename}.calculate_mapping_stats.log"
		shell:
			"\n\t\t("+module_load("python","bedtools","samtools")+
			"""
			samtools view -F 0x100 -b {input.bam} | calculate_mappingstatistics.py -b {PROJECT} -f -m -l -o {ALIGNER_CFG[program]}/report -s Complete -i {LIBCOMPLEXITY_CFG[subsample_size]}) >& {log}
			"""


rule index_bam:
	input:
		ALIGNER_CFG["program"] + "/{basename}.bam"
	output:
		ALIGNER_CFG["program"] + "/{basename}.bam.bai"
	log:
		ALIGNER_CFG["program"] + "/log/{basename}.bam.bai.log"
	shell:
		"\n\t\t("+module_load("samtools")+
		"""
		samtools index {input}) >& {log}
		"""	

# BWA #####

rule run_bwa_single:
	input:
		"fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz"
	output:
		bam="bwa/{basename}.bam"
	log:
		"bwa/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bwa","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		{ALIGNER_CFG[binary]} mem -R '@RG\\tID:'$RG'\\tSM:'$SM'\\tLB:'$LB'\\tPL:ILLUMINA' -t `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} {input} | \
		samtools sort -O bam -T {TMP_DIR}/{wildcards.basename} -o {output} -@ 2 -) >& {log}
		"""

rule run_bwa_paired:
	input:
		fq1="fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz",
		fq2="fastq/{basename}_R2.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R2.fastq.gz"
	output:
		bam="bwa/{basename}.bam"
	log:
		"bwa/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bwa","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		{ALIGNER_CFG[binary]} mem -R '@RG\\tID:'$RG'\\tSM:'$SM'\\tLB:'$LB'\\tPL:ILLUMINA' -t `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} {input.fq1} {input.fq2} | \
		samtools sort -O bam -T {TMP_DIR}/{wildcards.basename} -o {output} -@ 2 -) >& {log}
		"""

# bowtie1 #####

rule run_bowtie1_single:
	input:
		"fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz"
	output:
		bam="bowtie1/{basename}.bam"
	log:
		"bwa/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie1","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		gunzip -cd {input} | {ALIGNER_CFG[binary]} -S --sam-RG ID:$RG --sam-RG LB:$LB --sam-RG SM:$SM -p `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} - | \
		samtools sort -O bam -T ${{TMPDIR:-.}}/{wildcards.basename} -o {output.bam} -@ 2 -) >& {log}
		"""

rule run_bowtie1_paired:
	input:
		fq1="fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz",
		fq2="fastq/{basename}_R2.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R2.fastq.gz"
	output:
		bam="bowtie1/{basename}.bam"
	log:
		"bwa/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie1","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		{ALIGNER_CFG[binary]} -S --sam-RG ID:$RG --sam-RG LB:$LB --sam-RG SM:$SM -p `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} -1 <( zcat {input.fq1} ) -2 <( zcat {input.fq2} ) | \
		samtools sort -O bam -T ${{TMPDIR:-.}}/{wildcards.basename} -o {output.bam} -@ 2 -) >& {log}
		"""


# gsnap #####

rule run_gsnap_single:
	input:
		fq="fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz"

	output:
		bam="gsnap/{basename}.bam"
	log:
		"gsnap/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("gmap","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		{ALIGNER_CFG[binary]} -D {ALIGNER_CFG[index_directory]} -d {ALIGNER_CFG[index]} --gunzip -A sam -t `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} --read-group-id=\"$RG\" --read-group-name=\"$SM\" --read-group-library=\"$LB\" --read-group-platform=illumina {input.fq} >  {TMP_DIR}/{wildcards.basename}.sam;
		sam_sort -D {ALIGNER_CFG[index_directory]} -d {ALIGNER_CFG[index]} --sort2=orig --mark-first  {TMP_DIR}/{wildcards.basename}.sam | samtools view -bS - > {output.bam}) >& {log}
		"""

rule run_gsnap_paired:
	input:
		fq1="fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz",
		fq2="fastq/{basename}_R2.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R2.fastq.gz",
	output:
		bam="gsnap/{basename}.bam"
		# transloc="gsnap/{basename}.transloc.bam"
	log:
		"gsnap/log/{basename}.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("gmap","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		{ALIGNER_CFG[binary]} -D {ALIGNER_CFG[index_directory]} -d {ALIGNER_CFG[index]} --gunzip -A sam -t `expr ${{NSLOTS:-{threads}}} - 2` {ALIGNER_CFG[parameter]} --read-group-id=\"$RG\" --read-group-name=\"$SM\" --read-group-library=\"$LB\" --read-group-platform=illumina {input.fq1} {input.fq2} > {TMP_DIR}/{wildcards.basename}.sam;
		sam_sort -D {ALIGNER_CFG[index_directory]} -d {ALIGNER_CFG[index]} --sort2=orig --mark-first  {TMP_DIR}/{wildcards.basename}.sam | samtools view -bS - > {output.bam}) >& {log}
		"""

		# sam_sort -D {ALIGNER_CFG[index_directory]} -d {ALIGNER_CFG[index]} --sort2=orig --mark-first {TMP_DIR}/{wildcards.basename}.sam | samtools view -bS - | java -jar /group/sequencing/Bfx/scripts/mathias/deep_seq_pipeline_java/jar/SplitGsnapTranslocation.jar I=/dev/stdin ON={output.bam} OT={output.transloc} SO=coordinate TMP_DIR={TMP_DIR}) >& {log}
