try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"

include: "do_data_cleaning.snakemake"

####################
# Config/Parameter #
####################

# miRNA reads usually have to be trimmed
if not CLEAN_DATA_CFG:
	raise WorkflowError("Please provide an clean_data configuration for preprocessing of reads!")

# reference configuration
if not REFERENCE_CFG:
	raise WorkflowError("Please provide a reference configuration!")

# annotation configuration
if not ANNOTATION_CFG:
	raise WorkflowError("Please provide an annotation configuration!")

# aligner configuration for bowtie is configured by default but aligner.index and aligner.index_directory can be explicitly changed
# moreover, three indices are needed: index (genome), index.rRNA (all rRNAs) and index.tRNA (all tRNAs)
ALIGNER_CFG["binary"] = "bowtie"
if "index_directory" not in ALIGNER_CFG:
	ALIGNER_CFG["index_directory"] = "/projects/seq-work/user/pipeline/bowtie1"
if "index" not in ALIGNER_CFG:
	ALIGNER_CFG["index"] = REFERENCE_CFG["version"]
if not os.path.exists(ALIGNER_CFG["index_directory"]+"/"+ALIGNER_CFG["index"]+".1.ebwt"):
	raise WorkflowError("Could not find the bowtie1 genome index files for "+ALIGNER_CFG["index"]+" (aligner.index) in "+ALIGNER_CFG["index_directory"]+" (aligner.index_directory)!")
if not os.path.exists(ALIGNER_CFG["index_directory"]+"/"+ALIGNER_CFG["index"]+".rRNA.1.ebwt"):
	raise WorkflowError("Could not find the bowtie1 rRNA index files for "+ALIGNER_CFG["index"]+".rRNA (aligner.index) in "+ALIGNER_CFG["index_directory"]+" (aligner.index_directory)!")
if not os.path.exists(ALIGNER_CFG["index_directory"]+"/"+ALIGNER_CFG["index"]+".tRNA.1.ebwt"):
	raise WorkflowError("Could not find the bowtie1 tRNA index files for "+ALIGNER_CFG["index"]+".tRNA (aligner.index) in "+ALIGNER_CFG["index_directory"]+" (aligner.index_directory)!")

# mirdeep_star configuration
MIRDEEP_STAR_CFG = config.get("mirdeep_star",dict())

if "genome_dir" not in MIRDEEP_STAR_CFG:
	if "version" in MIRDEEP_STAR_CFG and os.path.exists("/projects/seq-work/user/pipeline/mirdeep_star/"+SPECIES+"/"+MIRDEEP_STAR_CFG["version"]):
		MIRDEEP_STAR_CFG["genome_dir"] = "/projects/seq-work/user/pipeline/mirdeep_star/"+SPECIES
	elif REFERENCE_CFG and "version" in REFERENCE_CFG and os.path.exists("/projects/seq-work/user/pipeline/mirdeep_star/"+SPECIES+"/"+REFERENCE_CFG["version"]):
		MIRDEEP_STAR_CFG["version"] = REFERENCE_CFG["version"]
		MIRDEEP_STAR_CFG["genome_dir"] = "/projects/seq-work/user/pipeline/mirdeep_star/"+SPECIES
	else:
		raise WorkflowError("Please provide the mirdeep* directory (which contains subdirectories of mirdeep* formatted genomes) by mirdeep_star.genome_dir.")
else:
	if "version" not in MIRDEEP_STAR_CFG and REFERENCE_CFG and "version" in REFERENCE_CFG and os.path.exists(MIRDEEP_STAR_CFG["genome_dir"]+"/"+REFERENCE_CFG["version"]):
		MIRDEEP_STAR_CFG["version"] = REFERENCE_CFG["version"]
	else:
		raise WorkflowError("Please provide the name of the mirdeep* formatted genome (which has to be a subdirectory of mirdeep_star.genome_dir) by mirdeep_star.version.")

if not os.path.exists(MIRDEEP_STAR_CFG["genome_dir"]+"/"+MIRDEEP_STAR_CFG["version"]):
	raise WorkflowError("Could not find the mirdeep* formatted genome directory "+MIRDEEP_STAR_CFG["version"]+" in the mirdeep* directory "+MIRDEEP_STAR_CFG["genome_dir"]+"!")

# path to mirdeep* jar
if "mirdeep_star" not in PROGRAM_CFG:
	PROGRAM_CFG["mirdeep_star"]="/group/sequencing/Bfx/scripts/extern/mirdeep_star/MD.jar"
		

###########
# Targets #
###########

MIRNA_ANALYSIS_DATA = []
for l in LIBRARIES:
	MIRNA_ANALYSIS_DATA.append("miRNA/"+l+".filtered.result.gz")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".mirdeep_star.csv")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".unfiltered_mapping.txt")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".filtered_mapping.txt")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".rRNA.txt")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".tRNA.txt")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".otherRNA.txt")
	MIRNA_ANALYSIS_DATA.append("miRNA/report/data/"+PROJECT+"_"+l+".miRNA_data.txt")

MIRNA_ANALYSIS_PLOTS = ["miRNA/report/pdf/" + PROJECT + "_number_of_profiled_miRNAs.pdf"]
MIRNA_ANALYSIS_TARGETS = MIRNA_ANALYSIS_DATA + MIRNA_ANALYSIS_PLOTS

#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

rule do_miRNA_analysis:
	input:
		CLEAN_DATA_TARGETS,
		MIRNA_ANALYSIS_TARGETS
		
### analyse/plot results with R ###
rule summarise_miRNA_analysis:
	input:
		MIRNA_ANALYSIS_DATA
	output:
		MIRNA_ANALYSIS_PLOTS
	log:
		"miRNA/log/summarise_miRNA_analysis.log"
	shell:
		"\n\t\t("+module_load("R")+
		"""
		summarise_miRNA_analysis.R {PROJECT} miRNA/report/data miRNA/report/pdf) >& {log}
		"""


### map to genome and analyse miRNA with mirdeep  ###
rule parse_mirdeep_star_results:
	input:
		"miRNA/{basename}.filtered.result.gz"
	output:
		report="miRNA/report/data/"+PROJECT+"_{basename}.mirdeep_star.csv"
	log:
		"miRNA/log/{basename}.parse_mirdeep_star_results.log"
	shell:
		"""
		gunzip -cd {input} | parse_mirdeep_star_results.pl > {output.report}
		"""

rule run_mirdeep_star:
	input:
		bam="miRNA/{basename}.filtered.bam",
		genome_dir="genome",
		targetscan_dir="targetScan"
	output:
		all="miRNA/{basename}.filtered.result.gz",
		cluster=temp("miRNA/{basename}.filtered.cluster"),
		known_result=temp("miRNA/{basename}.filtered.known_miR.result"),
		result=temp("miRNA/{basename}.filtered.result"),
		sorted_bam="miRNA/{basename}.filtered.sorted.bam"
	log:
		"miRNA/log/{basename}.run_mirdeep_star.log"
	shell:
		"""
		(java -Xmx16G -jar {PROGRAM_CFG[mirdeep_star]} -m 5 -t 18 -l 25 -g {REFERENCE_CFG[version]} {input.bam}) >& {log}
		gzip -c miRNA/{wildcards.basename}.filtered.result > {output.all}
		"""

rule link_mirdeep_star_directories:
	input:
		genome_dir="/projects/seq-work/user/pipeline/mirdeep_star/"+SPECIES,
		targetscan_dir="/group/sequencing/Bfx/scripts/extern/mirdeep_star/targetScan"
	output:
		genome_dir=temp("genome"),
		targetscan_dir=temp("targetScan")
	shell:
		"""
		ln -snf {input.genome_dir} {output.genome_dir}
		ln -snf {input.targetscan_dir} {output.targetscan_dir}
		"""

rule map_filtered_miRNA_fastq_on_genome:
	input:
		fq="miRNA/{basename}_R1.filtered.fastq.gz"
	output:
		bam="miRNA/{basename}.filtered.bam",
		bai="miRNA/{basename}.filtered.bam.bai",
		summary="miRNA/report/data/"+PROJECT+"_{basename}.filtered_mapping.txt"
	log:
		"miRNA/log/{basename}.map_filtered_miRNA_fastq_on_genome.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		gunzip -cd {input.fq} | {ALIGNER_CFG[binary]} -l 15 -v 2 -e 999 --nomaqround --best --strata -a -m 100 --chunkmbs 256 -p ${{NSLOTS:-{threads}}} -S --sam-RG ID:$RG --sam-RG LB:$LB --sam-RG SM:$SM {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} - | \
		samtools sort -O bam -T ${{TMPDIR:-.}}/{wildcards.basename} -o {output.bam} -@ 2 -;samtools index {output.bam}) 2>&1 | tee {output.summary} >& {log}
		"""


### filter miRNA fastq data (remove tRNA, rRNA, ncRNA and mRNA) ###
rule filter_otherRNA_from_miRNA_fastq:
	input:
		fq="miRNA/{basename}_R1.wo_rRNA_tRNA.fastq.gz",
		bed="miRNA/otherRNA_genomic_filter.bed"
	output:
		fq=temp("miRNA/{basename}_R1.filtered.fastq.gz"),
		bam=temp("miRNA/{basename}.wo_rRNA_tRNA.bam"),
		summary="miRNA/report/data/"+PROJECT+"_{basename}.otherRNA.txt",
		clean="miRNA/report/data/"+PROJECT+"_{basename}.miRNA_data.txt"
	log:
		"miRNA/log/{basename}.filter_otherRNA_from_miRNA_fastq.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie","samtools","bedtools")+
		"""
		gunzip -cd {input.fq} | {ALIGNER_CFG[binary]} -l 15 -n 0 -e 999 --nomaqround --best --strata -a -m 1 --chunkmbs 256 -p ${{NSLOTS:-{threads}}} -S {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} - | samtools sort -O bam -T ${{TMPDIR:-.}}/{wildcards.basename} -o {output.bam} -@ 2 -
		bedtools intersect -a {output.bam} -b {input.bed} -s -v -sorted -g {REFERENCE_CFG[path]}.genome | bedtools bamtofastq -i stdin -fq /dev/stdout | gzip -c > {output.fq}
		bedtools intersect -a {output.bam} -b {input.bed} -s -wa -wb -bed -sorted -g {REFERENCE_CFG[path]}.genome | awk '{{OFS="\t";print $4,$3-$2,$16}}' | \
		sort -k 1,1 | bedtools groupby -g 1,2 -c 3 -o first | awk '{{print $3,$2}}' | sort -k 1,1 -k 2,2n | uniq -c | awk 'BEGIN{{OFS="\\t";print "type","length","count"}}{{print $2,$3,$1}}' > {output.summary}
		gunzip -cd {output.fq} | awk 'NR%4==2{{print length($0)}}' | sort -n | uniq -c | awk 'BEGIN{{OFS="\\t";print "type","length","count"}}{{print "clean",$2,$1}}' > {output.clean}) >& {log}
		"""	

rule create_otherRNA_genomic_filter_bed:
	output:
		"miRNA/otherRNA_genomic_filter.bed"
	log:
		"miRNA/log/create_otherRNA_genomic_filter_bed.log"
	shell:
		"\n\t\t("+module_load("bedtools","cufflinks","perl")+
		"""
		cat {ANNOTATION_CFG[gtf]} | egrep --color=never 'gene_biotype "(Mt_rRNA|Mt_tRNA|rRNA|scaRNA|snoRNA|snRNA|sRNA|protein_coding)"' | gffread -o- -F - | \
		sed -r 's|(.*\\t).*gene_biotype=([^;]+).*|\\1\\2|p' | awk '$3=="transcript" || $3=="mRNA"{{OFS="\\t";print $1,$4,$5,$9,$6,$7}}' | bedtools merge -c 4,5,6 -o first,first,first | \
		bedtools sort -i stdin -faidx {REFERENCE_CFG[path]}.genome > {output}) >& {log}
		"""

rule filter_tRNA_from_miRNA_fastq:
	input:
		"miRNA/{basename}_R1.wo_rRNA.fastq.gz"
	output:
		fq=temp("miRNA/{basename}_R1.wo_rRNA_tRNA.fastq.gz"),
		summary="miRNA/report/data/"+PROJECT+"_{basename}.tRNA.txt"
	log:
		"miRNA/log/{basename}.filter_tRNA_from_miRNA_fastq.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie")+
		"""		
		gunzip -cd {input} | {ALIGNER_CFG[binary]} -l 15 -n 0 --nomaqround -p ${{NSLOTS:-{threads}}} -k 1 -S --sam-nohead --un=miRNA/{wildcards.basename}_R1.wo_rRNA_tRNA.fastq {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]}.tRNA - /dev/stdout | awk '$3!="*"{{print length($10)}}' | sort | uniq -c | awk 'BEGIN{{OFS="\\t";print "type","length","count"}}{{print "tRNA",$2,$1}}' > {output.summary}) >& {log}
		gzip miRNA/{wildcards.basename}_R1.wo_rRNA_tRNA.fastq
		"""

rule filter_rRNA_from_miRNA_fastq:
	input:
		"fastq/{basename}_R1.clean.fastq.gz"
	output:
		fq=temp("miRNA/{basename}_R1.wo_rRNA.fastq.gz"),
		summary="miRNA/report/data/"+PROJECT+"_{basename}.rRNA.txt"
	log:
		"miRNA/log/{basename}.filter_rRNA_from_miRNA_fastq.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie")+
		"""	
		gunzip -cd {input} | {ALIGNER_CFG[binary]} -l 15 -n 0 --nomaqround -p ${{NSLOTS:-{threads}}} -k 1 -S --sam-nohead --un=miRNA/{wildcards.basename}_R1.wo_rRNA.fastq {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]}.rRNA - /dev/stdout | awk '$3!="*"{{print length($10)}}' | sort | uniq -c | awk 'BEGIN{{OFS="\\t";print "type","length","count"}}{{print "rRNA",$2,$1}}' > {output.summary}) >& {log}
		gzip miRNA/{wildcards.basename}_R1.wo_rRNA.fastq
		"""

rule map_unfiltered_miRNA_fastq_on_genome:
	input:
		fq="fastq/{basename}_R1.clean.fastq.gz" if CLEAN_DATA_CFG else "fastq/{basename}_R1.fastq.gz"
	output:
		bam=temp("miRNA/{basename}.unfiltered.bam"),
		summary="miRNA/report/data/"+PROJECT+"_{basename}.unfiltered_mapping.txt"
	log:
		"miRNA/log/{basename}.map_unfiltered_miRNA_fastq_on_genome.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("bowtie","samtools")+
		"""
		RG="{wildcards.basename}";LB="{wildcards.basename}";SM="{wildcards.basename}"
		if [[ "{wildcards.basename}" =~ ^(L[0-9]+)_(.+) ]];then LB="${{BASH_REMATCH[1]}}";SM="${{BASH_REMATCH[2]}}";fi
		gunzip -cd {input.fq} | {ALIGNER_CFG[binary]} -l 15 -v 2 -e 999 --nomaqround --best --strata -a -m 1 --chunkmbs 256 -p ${{NSLOTS:-{threads}}} -S --sam-RG ID:$RG --sam-RG LB:$LB --sam-RG SM:$SM {ALIGNER_CFG[index_directory]}/{ALIGNER_CFG[index]} - | \
		samtools sort -O bam -T ${{TMPDIR:-.}}/{wildcards.basename} -o {output.bam} -@ 2 -) 2>&1 | tee {output.summary} >& {log}
		"""

