try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"

####################
# Config/Parameter #
####################

BAM_FILES = [ALIGNER_CFG["program"] + "/" + s + ".bam" for s in LIBRARIES]
BAI_FILES = [ALIGNER_CFG["program"] + "/" + s + ".bai" for s in LIBRARIES]

###########
# Targets #
###########

FEATURECOUNTS_TABLE = "genecount/" + PROJECT + "." + REFERENCE_CFG["version"] + ".e" + ANNOTATION_CFG["version"] + ".txt"
FEATURECOUNTS_SUMMARY ="genecount/" + PROJECT + "." + REFERENCE_CFG["version"] + ".e" + ANNOTATION_CFG["version"] + ".txt.summary"
FEATURECOUNTS_OUT = "genecount/" + PROJECT + "." + REFERENCE_CFG["version"] + ".e" + ANNOTATION_CFG["version"] + ".out"
FEATURECOUNTS_STATS = ["report/data/" + PROJECT + "_featureCounts_stats_featurecounts_1.csv","report/" + PROJECT + "_featureCounts_stats_featurecounts_1.tex"]
FEATURECOUNTS_COMPLEXITY = ["genecount/report/data/" + PROJECT + "_featureCounts_complexity.csv","genecount/report/pdf/" + PROJECT + "_featureCounts_isoformcomplexity_complete_1.Genes.pdf","genecount/report/pdf/" + PROJECT + "_featureCounts_isoformcomplexity_added_1.Genes.pdf","genecount/report/png/" + PROJECT + "_featureCounts_isoformcomplexity_complete_1.Genes.png","genecount/report/png/" + PROJECT + "_featureCounts_isoformcomplexity_added_1.Genes.png"]
GENECOUNTS_TARGETS = [FEATURECOUNTS_TABLE,FEATURECOUNTS_SUMMARY,FEATURECOUNTS_OUT,FEATURECOUNTS_STATS,FEATURECOUNTS_COMPLEXITY]


#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

# simple rules are run on the head node
localrules: extract_featureCounts_stats

rule do_genecounts:
	input:
		GENECOUNTS_TARGETS

rule calculate_featureCounts_isoformcomplexity:
	input:
		table=FEATURECOUNTS_TABLE,
		summary=FEATURECOUNTS_SUMMARY
	output:
		FEATURECOUNTS_COMPLEXITY
	log:
		"genecount/log/calculate_featureCounts_isoformcomplexity.log"
	params:
		lib_display="-d "+LIBCOMPLEXITY_CFG["lib_display"] if "lib_display" in LIBCOMPLEXITY_CFG else ""
	shell:
		"\n\t\t("+module_load("python")+
		"""
		calculate_isoformcomplexity.py -f {input.table} -s {input.summary} -r {LIBCOMPLEXITY_CFG[gene_relation_txt]} -b {PROJECT} -i {LIBCOMPLEXITY_CFG[subsample_size]} -o genecount/report -c Genes {params.lib_display}) >& {log}
		"""

rule extract_featureCounts_stats:
	input:
		FEATURECOUNTS_SUMMARY
	output:
		FEATURECOUNTS_STATS
	log:
		"genecount/log/extract_featureCounts_stats.log"
	shell:
		"\n\t\t("+module_load("python")+
		"""
		extract_featurecounts.py  -f {input} -b {PROJECT} -r report) >& {log}
		"""

rule run_featureCounts:
	input:
		bams=BAM_FILES,
		bais=BAI_FILES
	output:
		table=FEATURECOUNTS_TABLE,
		out=FEATURECOUNTS_OUT,
		summary=FEATURECOUNTS_SUMMARY
	log:
		"genecount/log/run_featureCounts.log"
	threads:
		8
	shell:
		"\n\t\t("+module_load("subread","samtools")+
		"""
		[ $(samtools view -f 1 -c {input.bams[0]}) -eq 0 ] && PARAM=\"\" || PARAM=\" -p -C -B \"		
		featureCounts -a {ANNOTATION_CFG[gtf]} -s {STRAND} $PARAM -o {output.table} -Q 1 -T ${{NSLOTS:-{threads}}} --tmpDir {TMP_DIR} {input.bams}) >& {log}
		cp {log} {output.out}		
		sed -i.original -e '2s/{ALIGNER_CFG[program]}\/L[0-9]\+_//g;  2s/\.bam//g' {output.table}
		sed -i.original -e 's/{ALIGNER_CFG[program]}\/L[0-9]\+_//g; s/\.bam//g' {output.summary}
		"""


