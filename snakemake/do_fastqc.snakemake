try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"


####################
# Config/Parameter #
####################

###########
# Targets #
###########

FASTQC_TARGETS = ["fastqc/report/pdf/"+PROJECT+"_fastqc_overview.pdf","fastqc/report/data/"+PROJECT+"_fastqc_overview.csv",expand("fastqc/{f}_fastqc.html",f=FASTQ_BASE),expand("fastqc/{f}_fastqc.zip",f=FASTQ_BASE)]

##########
# Checks #
##########


#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

# simple rules are run on the head node
localrules: extract_fastqc_txt

rule do_fastqc:
	input:
		FASTQC_TARGETS		

rule summarise_fastqc:
	input:
		expand("fastqc/report/data/{f}_fastqc.txt",f=FASTQ_BASE)
	output:
		"fastqc/report/pdf/"+PROJECT+"_fastqc_overview.pdf",
		"fastqc/report/data/"+PROJECT+"_fastqc_overview.csv"
	log:
		"fastqc/log/summarise_fastqc.log"
	shell:
		"\n\t\t("+module_load("R")+
		"""
		summarise_fastqc.R {PROJECT} fastqc/report/data fastqc/report/pdf) >& {log}
		"""
		

rule extract_fastqc_txt:
	input:
		"fastqc/{basename}_fastqc.zip"
	output:
		temp("fastqc/report/data/{basename}_fastqc.txt")
	shell:
		"""
		unzip -p {input} {wildcards.basename}_fastqc/fastqc_data.txt > {output}
		"""

rule run_fastqc:
	input:
		"fastq/{basename}.fastq.gz"
	output:
		"fastqc/{basename}_fastqc.zip",
		"fastqc/{basename}_fastqc.html"
	log:
		"fastqc/log/{basename}.log"
	shell:
		"\n\t\t("+module_load("fastqc")+
		"""
		fastqc -o fastqc {input}) >& {log}
		"""


