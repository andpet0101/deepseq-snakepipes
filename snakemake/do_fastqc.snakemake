try:
	GENERAL_SETTINGS_INCLUDED
except NameError: 
	include: "general_settings.snakemake"


####################
# Config/Parameter #
####################

###########
# Targets #
###########

FASTQC_TARGETS = []

for stat in ["fastqc_summary","per_base_sequence_quality","per_sequence_quality_scores","per_base_sequence_content","per_sequence_gc_content","per_base_n_content","sequence_length_distribution",
"total_deduplicated_levels","sequence_duplication_levels","overrepresented_sequences","adapter_content","total_adapter_content","kmer_content"]:
	FASTQC_TARGETS.append("fastqc/report/pdf/"+PROJECT+"_"+stat+".pdf")
	FASTQC_TARGETS.append("fastqc/report/data/"+PROJECT+"_"+stat+".csv")


##########
# Checks #
##########


#########
# Rules #
#########

shell.prefix("set -euf -o pipefail;")

# simple rules are run on the head node
localrules: extract_fastqc_txt

rule do_fastqc:
	input:
		FASTQC_TARGETS		

rule summarise_fastqc:
	input:
		expand("fastqc/report/data/{f}_fastqc.txt",f=FASTQ_BASE)
	output:
		FASTQC_TARGETS
	log:
		"fastqc/log/summarise_fastqc.log"
	shell:
		"\n\t\t("+module_load("R")+
		"""
		summarise_fastqc.R {PROJECT} fastqc/report/data fastqc/report/pdf) >& {log}
		"""
		

rule extract_fastqc_txt:
	input:
		"fastqc/{basename}_fastqc.zip"
	output:
		temp("fastqc/report/data/{basename}_fastqc.txt")
	shell:
		"""
		unzip -p {input} {wildcards.basename}_fastqc/fastqc_data.txt > {output}
		"""

rule run_fastqc:
	input:
		"fastq/{basename}.fastq.gz"
	output:
		"fastqc/{basename}_fastqc.zip",
		"fastqc/{basename}_fastqc.html"
	log:
		"fastqc/log/{basename}.log"
	shell:
		"\n\t\t("+module_load("fastqc")+
		"""
		fastqc -o fastqc -a /group/sequencing/Bfx/scripts/common/dsp/dsp/files/fastqc_adapter.txt {input}) >& {log}
		"""


